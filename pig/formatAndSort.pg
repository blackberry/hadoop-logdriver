/*
# Copyright 2013 BlackBerry, Inc.
#
#  Licensed under the Apache License, Version 2.0 (the "License");
#  you may not use this file except in compliance with the License.
#  You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
#  Unless required by applicable law or agreed to in writing, software
#  distributed under the License is distributed on an "AS IS" BASIS,
#  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#  See the License for the specific language governing permissions and
#  limitations under the License. 
*/

DEFINE FirstItemOnlyStoreFunc com.rim.logdriver.pig.FirstItemOnlyStoreFunc();
DEFINE DateFormatter          com.rim.logdriver.pig.DateFormatter('$dateFormat');

lines = LOAD '$tmpdir/rawlines'
        USING PigStorage('\\x$fs')
        AS (time:long, message:chararray, eventId:int, createTime:long, blockNo:long, lineNo:long);

SPLIT lines INTO
  bad_data IF (
    (time is null)
    OR (createTime is null)
    OR (blockNo is null)
    OR (lineNo is null)
  ),
  good_data IF (
    (time is not null)
    AND (createTime is not null)
    AND (blockNo is not null)
    AND (lineNo is not null)
  );  

STORE bad_data INTO '$tmpdir/bad_data';


message_first = FOREACH good_data GENERATE CONCAT(DateFormatter(time), CONCAT( ' ', message)), time, createTime, blockNo, lineNo;
  
ordered_lines = ORDER message_first BY time, createTime, blockNo, lineNo;

STORE ordered_lines INTO '$out' USING FirstItemOnlyStoreFunc;
